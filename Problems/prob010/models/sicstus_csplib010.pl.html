
<!DOCTYPE html>
<html lang="en">
<head>
	<meta charset="utf-8">
	
	<meta name="viewport" content="width=device-width, initial-scale=1.0">
	<meta name="description" content="">
	<meta name="author" content="">
	<!-- <link rel="shortcut icon" href="../../assets/ico/favicon.png"> -->

	<title>010: Social Golfers Problem</title>

	<!-- Bootstrap core CSS -->
	<link href="/bootstrap/css/bootstrap.css" rel="stylesheet">

	<link href="/css/main.css" rel="stylesheet">
	<script src="//ajax.googleapis.com/ajax/libs/jquery/1.11.0/jquery.min.js" ></script>

	<!-- Enable table sorting -->
	<script src="/js/tablesorter/jquery.tablesorter.min.js"></script>
	<link rel="stylesheet" href="/js/tablesorter/blue/style.css" type="text/css"/>
	<script type="text/javascript">
	$(function() {
		$(".tablesorter").tablesorter();
	});
	</script>

	<script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.0/MathJax.js?config=TeX-AMS_HTML.js">
	MathJax.Hub.Config({
	  	tex2jax: {
	    inlineMath: [['$','$'], ['\\(','\\)']],
	    processEscapes: true
	  	}
	});
	</script>

  
	<link type="text/css" rel="stylesheet" href="/syntax_highlighter/shCoreDefault.css"/>

</head>

<body>

	<ol class="breadcrumb">
    	  <li><a href="/">CSPLib</a></li>

  <li><a href="/Problems">Problems</a></li>
  <li><a href="/Problems/prob010">prob010</a></li>

  <li><a href="/Problems/prob010/models">Models</a></li>
  <li>sicstus_csplib010.pl</li>
	</ol>

	<div class="container">

		<div class="page-header">
			<h1>010: Social Golfers Problem</h1>


<!-- macro to Not made a link if on the page the link links to -->


<header class="nav-header">
<div class="nav-container">
  <nav class="nav">
  <ul>
     	<li><a href="/Problems/prob010/">Specification</a></li>

     	<li><a href="/Problems/prob010/data">Data files</a></li>

     	<li><a href="/Problems/prob010/results">Results</a></li>

     	<li><a href="/Problems/prob010/references">References</a></li>

     	<li><a href="/Problems/prob010/models">Models</a></li>

     	<li><a href="/Problems/prob010/cite">Cite</a></li>

    <li><a href="/Problems/prob010.json" >Json</a></li>

    <li style="float:right"><a href="https://github.com/csplib/csplib/blob/master/Problems/prob010/models/sicstus_csplib010.pl">Edit Page</a></li>
  </ul>
  </nav>
</div>
</header>


		</div>


<a href="/Problems/prob010/models/sicstus_csplib010.pl">Download</a>
<pre >/*
 * SICSTUS CLPFD DEMONSTRATION PROGRAM
 * Purpose   : Social Golfer Problem
 * Author    : Mats Carlsson
 *
 * We have 32 golfers, individual play.
 * We will golf for W weeks.
 * Set up the foursomes so that each person only golfs with the same
 * person once.
 *
 * | ?- golf(8,4,9,[min],bycolall,bounds).
 */

:- module(golf, [golf/6]).

:- use_module(library(lists)).
:- use_module(library(clpfd)).

golf(G, S, W, LabelOpt, VarOrder, Consistency) :-
	Opt = [consistency(Consistency)],
	golfer(G, S, W, Schedule, Byrow, Bycol, Opt),
	var_order(VarOrder, Byrow, Bycol, All),
	(   foreach(Set,All),
	    param(LabelOpt)
	do  labeling(LabelOpt, Set)
	),
	(   foreach(Round,Schedule),
	    count(Wk,1,_)
	do  format('Week ~d:\n', [Wk]),
	    (   foreach(Four,Round)
	    do  format('                    ~d ~d ~d ~d\n', Four)
	    )
	).

var_order(bycol, _, All, All).
var_order(byrow, All, _, All).
var_order(bycolall, _, Cols, [All]) :-
	append(Cols, All).
var_order(byrowall, Rows, _, [All]) :-
	append(Rows, All).

golfer(G, S, W, Schedule, PlayersByRow, PlayersByCol, Opt) :-
	schedule(0, G, S, W, Schedule, PlayersByRow, PlayersByCol, Opt),
	Schedule = [FirstS|RestS],
	append(FirstS, Players),
	labeling([enum], Players), !,
	(   foreach(Week,RestS),
	    param(S)
	do  (   foreach([P|Ps],Week),
		param(S)
	    do  P/S #= Q0,
		(   foreach(P1,Ps),
		    fromto(Q0,Q1,Q2,_),
		    param(S)
		do  P1/S #= Q2,
		    Q1 #&lt; Q2
		)
	    ),
	    seed_week(0, S, Week)
	),
	ordered_players_by_week(PlayersByRow),
	players_meet_disjoint(Schedule, G, S, Opt),
	first_s_alldiff(0, S, RestS, Opt).


schedule(W, _, _, W, [], [], [], _) :- !.
schedule(I, G, S, W, [Week|Schedule], [ByRow|ByRows], [ByCol|ByCols], Opt) :-
	(   for(_,1,G),
	    foreach(Group,Week),
	    param([G,S])
	do  length(Group, S),
	    GS is G*S-1,
	    domain(Group, 0, GS)
	),
	append(Week, ByRow),
	all_different(ByRow, Opt),
	transpose(Week, WeekT),
	append(WeekT, ByCol),
	J is I+1,
	schedule(J, G, S, W, Schedule, ByRows, ByCols, Opt).

players_meet_disjoint(Schedule, G, S, Opt) :-
	append(Schedule, Groups),
	groups_meets(Groups, Tuples, [], MeetVars, []),
	GS is G*S,
	(   foreach([A,B,C],Tuples),
	    param([GS,Opt])
	do  scalar_product([GS,1], [A,B], #=, C, Opt)
	),
	all_distinct(MeetVars, Opt).

groups_meets([], Tuples, Tuples) --&gt; [].
groups_meets([Group|Groups], Tuples1, Tuples3) --&gt;
	group_meets(Group, Tuples1, Tuples2),
	groups_meets(Groups, Tuples2, Tuples3).

group_meets([], Tuples, Tuples) --&gt; [].
group_meets([P|Ps], Tuples1, Tuples3) --&gt;
	group_meets(Ps, P, Tuples1, Tuples2),
	group_meets(Ps, Tuples2, Tuples3).

group_meets([], _, Tuples, Tuples) --&gt; [].
group_meets([Q|Qs], P, [[P,Q,PQ]|Tuples1], Tuples2) --&gt; [PQ],
	group_meets(Qs, P, Tuples1, Tuples2).

seed_week(S, S, Week) :- !,
	S1 is S-1,
	seed_week(Week, S1).
seed_week(I, S, [[I|_]|Week]) :-
	J is I+1,
	seed_week(J, S, Week).

seed_week([], _).
seed_week([[J|_]|Week], I) :-
	I #&lt; J,
	seed_week(Week, J).

ordered_players_by_week([W|Ws]) :-
	ordered_players_by_week(Ws, W).

ordered_players_by_week([], _).
ordered_players_by_week([W|Ws], V) :-
	W = [_,Y|_],
	V = [_,X|_],
	X #&lt; Y,
	ordered_players_by_week(Ws, W).

first_s_alldiff(S, S, _Schedule, _) :- !.
first_s_alldiff(I, S, Schedule, Opt) :-
	(   foreach(Week,Schedule),
	    foreach(Ith,Part),
	    param(I)
	do  nth0(I, Week, [_|Ith])
	),
	append(Part, Conc),
	all_different(Conc, Opt),
	J is I+1,
	first_s_alldiff(J, S, Schedule, Opt).


</pre>


	</div>


<!-- Syntax highlighter  -->
<script type="text/javascript" src="/syntax_highlighter/shCore.js"></script>
<script type="text/javascript" src="/syntax_highlighter/shAutoloader.js" ></script>
<script type="text/javascript">
    SyntaxHighlighter.autoloader(

    // Languages that have a language page in csplib
    // Only the Title from specification.md converted to lower case should be used.
    ['comet',        '/js/shBrushComet.js'],
    ['eclipse',      '/js/shBrushECLiPSe.js'],
    ['essence',      '/js/shBrushEssence.js'],
    ['essenceprime', '/js/shBrushEssencePrime.js'],
    ['minizinc',     '/js/shBrushMiniZinc.js'],


    // Standard Programming languages
    ['cpp','hpp','hh','cc','h','c', '/syntax_highlighter/shBrushCpp.js'],
    ['erl','hrl', '/syntax_highlighter/shBrushErlang.js'] ,
    ['cs'       , '/syntax_highlighter/shBrushCSharp.js'] ,
    ['groovy'   , '/syntax_highlighter/shBrushGroovy.js'] ,
    ['java'     , '/syntax_highlighter/shBrushJava.js']   ,
    ['js'       , '/syntax_highlighter/shBrushJScript.js'],
    ['php'      , '/syntax_highlighter/shBrushPhp.js']    ,
    ['pl'       , '/syntax_highlighter/shBrushPerl.js']   ,
    ['py'       , '/syntax_highlighter/shBrushPython.js'] ,
    ['rb'       , '/syntax_highlighter/shBrushRuby.js']   ,
    ['scala'    , '/syntax_highlighter/shBrushScala.js']  ,
    ['xml'      , '/syntax_highlighter/shBrushXml.js']
 	);

	SyntaxHighlighter.defaults['toolbar'] = false;
  SyntaxHighlighter.all();
</script>


</div>

<div id="footer">

    <center><a rel="license" href="http://creativecommons.org/licenses/by/4.0/"><img alt="Creative Commons License" style="border-width:0" src="https://i.creativecommons.org/l/by/4.0/80x15.png" /></a> This work is licensed under a <a rel="license" href="http://creativecommons.org/licenses/by/4.0/">Creative Commons Attribution 4.0 International License</a>. </center>

</div>
  <!-- float footer to bottom of page -->
  <script>

    $(document).ready(function() {

     var docHeight = $(window).height();
     var footerHeight = $('#footer').height();
     var footerTop = $('#footer').position().top + footerHeight;

     if (footerTop + 20 < docHeight) {
      $('#footer').css('margin-top',  (docHeight - (footerTop + 20)) + 'px');
     }
    });
   </script>
</body>

</html>