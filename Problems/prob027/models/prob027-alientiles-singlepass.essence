language ESSENCE 1.3.0

$ Problem Alien Tiles
$
$ Problem details available at http://www.csplib.org/Problems/prob027/
$
$ Essence model by Andrew Martin
$
$ Licenced under CC-BY-4.0 : http://creativecommons.org/licenses/by/4.0/

given gridSize : int(1..)
given colours : int(1..)

letting dGridSize be domain int(1..gridSize)
letting dColours be domain int(0..colours-1)

$ colours moves is always equal to no moves
find clickMatrix : matrix indexed by [dGridSize, dGridSize] of dColours

maximising (sum i,j : dGridSize . clickMatrix[i][j])

such that
    forAll index : int(1..gridSize-1) .

        $ removing symmetry
        clickMatrix[index][..] <=lex clickMatrix[index+1][..]
        /\
        clickMatrix[..][index] <=lex clickMatrix[..][index+1]

such that
    $ no way of reaching same state faster exists
    forAll simplerClickMatrix : matrix indexed by [dGridSize, dGridSize] of dColours .
    
        forAll index : int(1..gridSize-1) . (

            $ removing symmetry
            simplerClickMatrix[index][..] <=lex simplerClickMatrix[index+1][..]
            /\
            simplerClickMatrix[..][index] <=lex simplerClickMatrix[..][index+1]
        )
        /\

        $ ensure not faster
        (sum i,j : dGridSize . clickMatrix[i][j]) <= (sum i,j : dGridSize . simplerClickMatrix[i][j])
        \/
        
        $ ensure not same (for each tile, check if sum of clicks effecting it %colours same as other clickmatrix)
        exists x,y : dGridSize .

            ((sum row : dGridSize . clickMatrix[row][y]) + (sum col : dGridSize . clickMatrix[x][col]) - clickMatrix[x][y]) % colours
            !=
            ((sum row : dGridSize . simplerClickMatrix[row][y]) + (sum col : dGridSize . simplerClickMatrix[x][col]) - simplerClickMatrix[x][y]) % colours

